def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:7.2.1'
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 31).toInteger()
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 31)
    }
    lintOptions {
        abortOnError false
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    maven {
        // Android JSC is installed from npm
        url "$rootDir/../node_modules/jsc-android/dist"
    }
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'  // From node_modules from RNGP
    implementation("com.facebook.react:react-android:0.79.2")
    implementation("com.facebook.react:hermes-android:0.79.2")
}

apply plugin: "maven-publish"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.BV'
                artifactId = 'LinearGradient'
                version = '1.0.0'

                from components.release
            }
        }
        repositories {
            mavenLocal()
        }
    }
}
