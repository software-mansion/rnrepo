plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'signing'
}

group = 'com.swmansion'
version = '1.0.6'

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:8.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

gradlePlugin {
    plugins {
        buildle {
            id = 'com.swmansion.buildle'
            implementationClass = 'com.swmansion.buildle.buildleplugin.AarAutomationPlugin'
            displayName = 'Buildle Plugin'
            description = 'Automates React Native AAR integration for faster builds'
        }
    }
}

publishing {
    publications {
        buildleArtefact(MavenPublication) {
            groupId = group
            artifactId = System.getenv("PACKAGE_NAME")
            version = "${System.getenv("LIB_VERSION")}-rn${System.getenv("RN_VERSION")}"
            artifact(file("${System.getenv("AAR_FILEPATH")}")) {
                extension 'aar' 
            }
            pom {
                withXml {
                    def pomPath = System.getenv("AAR_FILEPATH").replace('.aar', '.pom')
                    def pomFile = file(pomPath) 
                    def existingPom = new XmlParser().parse(pomFile)
                    def rootNode = asNode()
                    existingPom.dependencies.dependency.each { dep ->
                        def dependenciesNode = rootNode.children().find { it.name() == 'dependencies' }
                        if (dependenciesNode == null) {
                            dependenciesNode = rootNode.appendNode('dependencies')
                        }
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dep.groupId.text())
                        dependencyNode.appendNode('artifactId', dep.artifactId.text())
                        dependencyNode.appendNode('version', dep.version.text())
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven { 
            name "reposiliteRepositoryReleases" 
            url "https://repo.swmtest.xyz/releases" 
            credentials {
                username = project.properties["mavenUser"] ?: System.getenv("MAVEN_USER")
                password = project.properties["mavenPassword"] ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications["buildleArtefact"]
}
