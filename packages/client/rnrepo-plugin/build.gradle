plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'signing'
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'org.rnrepo.tools'

// Fixed version in code - automatically appends -SNAPSHOT for local publishes
def baseVersion = '0.0.4'
def isLocalPublish = project.gradle.startParameter.taskNames.any {
    it.contains("MavenLocal")
}
version = baseVersion + (isLocalPublish ? '-SNAPSHOT' : '')

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
}

dependencies {
    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:8.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation gradleTestKit()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// Generate build-time constants
def generatedSourcesDir = file("$buildDir/generated/sources/kotlin/main")
sourceSets.main.kotlin.srcDirs += generatedSourcesDir

task generateBuildConstants {
    def outputDir = file(generatedSourcesDir)
    def outputFile = file("$outputDir/org/rnrepo/tools/prebuilds/BuildConstants.kt")
    def projectVersion = project.version.toString()

    outputs.dir(outputDir)
    outputs.file(outputFile)

    doLast {
        outputDir.mkdirs()
        outputFile.parentFile.mkdirs()

        outputFile.text = """package org.rnrepo.tools.prebuilds

/**
 * Build-time constants generated during plugin compilation.
 * DO NOT EDIT - This file is auto-generated.
 */
object BuildConstants {
    const val PLUGIN_VERSION = "${projectVersion}"
}
"""
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    dependsOn generateBuildConstants
    kotlinOptions {
        jvmTarget = "11"
    }
}

gradlePlugin {
    plugins {
        prebuildsPlugin {
            id = "org.rnrepo.tools.prebuilds-plugin"
            implementationClass = "org.rnrepo.tools.prebuilds.PrebuildsPlugin"
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = "RNRepo Gradle Plugin"
                description = "A Gradle plugin to automate React Native AAR prebuilds integration for faster builds"
                url = "https://rnrepo.org"
                licenses {
                    license {
                        name = "ISC"
                        url = "https://opensource.org/licenses/ISC"
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = "rnrepo"
            url = project.properties["mavenRepositoryUrl"]
            credentials {
                username = project.properties["mavenUsername"]
                password = project.properties["mavenPassword"]
            }
        }
    }
}

// Signing is required only when publishing to remote repository
signing {
    def publishingToMavenLocal = project.gradle.startParameter.taskNames.any { it.contains("MavenLocal") }
    setRequired(!publishingToMavenLocal)
    useInMemoryPgpKeys(project.findProperty("signingKey"), project.findProperty("signingPassword"))
    sign publishing.publications.release
}

tasks.test {
    useJUnitPlatform()
}

if (project == rootProject) {
    apply from: 'spotless.gradle'
}
