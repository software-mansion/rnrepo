plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'signing'
}

group = 'org.rnrepo.prebuilds'
version = project.findProperty("pluginVersion") ?: "0.0.1-MISSINGVERSION"

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:8.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

gradlePlugin {
    plugins {
        rnrepoPlugin {
            id = "org.rnrepo.prebuilds.rnrepo-plugin"
            implementationClass = "org.rnrepo.prebuilds.rnrepoplugin.AarAutomationPlugin"
        }
    }
}

publishing {
    publications {
        RNRepoPlugin(MavenPublication) {
            from components.java
            pom {
                name = "RNRepo Gradle Plugin"
                description = "A Gradle plugin to automate React Native AAR prebuilds integration for faster builds"
                url = "" // todo(rolkrado): add url
                licenses {
                    license {
                        name = "ISC"
                        url = "https://opensource.org/licenses/ISC"
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven { 
            name "DevelopmentRepository"
            url "https://repo.swmtest.xyz/releases"
            credentials {
                username = project.properties["mavenUserDev"]
                password = project.properties["mavenPasswordDev"]
            }
        }
        maven {
            name "ProductionRepository"
            url "https://packages.rnrepo.org/releases"
            credentials {
                username = project.properties["mavenUserProd"]
                password = project.properties["mavenPasswordProd"]
            }
        }
    }
}

// Signing is required only when publishing to remote repository
signing {
    def publishingToMavenLocal = project.gradle.startParameter.taskNames.any { it.contains("MavenLocal") }
    setRequired(!publishingToMavenLocal)
    useInMemoryPgpKeys(project.findProperty("signingKey"), project.findProperty("signingPassword"))
    sign publishing.publications.RNRepoPlugin
}
